/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=preparedZeroX);
    Not16(in=preparedZeroX, out=notPreparedZeroX);
    Mux16(a=preparedZeroX, b=notPreparedZeroX, sel=nx, out=preparedX);

    Mux16(a=y, b=false, sel=zy, out=preparedZeroY);
    Not16(in=preparedZeroY, out=notPreparedZeroY);
    Mux16(a=preparedZeroY, b=notPreparedZeroY, sel=ny, out=preparedY);

    Add16(a=preparedX, b=preparedY, out=XSumY);
    And16(a=preparedX, b=preparedY, out=XAndY);
    Mux16(a=XAndY, b=XSumY, sel=f, out=rawOut);
    Not16(in=rawOut, out=notRawOut);
    Mux16(a=rawOut, b=notRawOut, sel=no, out=out, out[0..7]=rightOut, out[8..15]=leftOut, out[15]=ng);

    Or8Way(in=leftOut, out=leftHasNotZero);
    Or8Way(in=rightOut, out=rightHasNotZero);
    Or(a=rightHasNotZero, b=leftHasNotZero, out=hasNotZero);
    Not(in=hasNotZero, out=zr);    
}